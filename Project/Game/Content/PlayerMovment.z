//Author Brandon Wolenetz

class PlayerMovment : ZilchComponent
{
    
    [Dependency]
    var Sprite : PlayerSprites;
    
    [Property]
    var MoveSpeed : Real = 10;
    
    [Property]
    var SprintMultiplier : Real = 2;
    
    [Property]
    var SprintSoundDist : Real = 5;
    
    var Dir : Real3 = Real3(0);
    
    var FreezeTimer : Real = 0;
    
    var StartSpd : Real;
    
    function Initialize(init : CogInitializer) {
        this.StartSpd = this.MoveSpeed;
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        GlobalVariables.Player = this.Owner;
    }

    function OnLogicUpdate(event : UpdateEvent) {
        
        if(this.FreezeTimer > 0) {
            this.FreezeTimer -= event.Dt;
            this.Owner.RigidBody.Velocity = Real3(0);
            return;
        }
        
        var vel = Real3(0);
        
        var inMngr = this.LevelSettings.InputManager;
        
        if(Zero.Keyboard.KeyIsDown(inMngr.North)) vel +=  ((Real3( 0,  1, 0)));
        if(Zero.Keyboard.KeyIsDown(inMngr.South)) vel +=  ((Real3( 0, -1, 0)));
        if(Zero.Keyboard.KeyIsDown(inMngr.West)) vel +=  ((Real3(-1,  0, 0)));
        if(Zero.Keyboard.KeyIsDown(inMngr.East)) vel +=  ((Real3( 1,  0, 0)));
        
        vel = Math.Normalize(vel);
        
        vel *= this.MoveSpeed;
        
        if(vel != Real3(0))
            this.Sprite.Dir = vel;
        
        if(vel != Real3(0))
            this.Dir = Math.Normalize(vel);
        
        if(Zero.Keyboard.KeyIsDown(Keys.Shift)){
            vel *= this.SprintMultiplier;
            if(GlobalVariables.UseWeapon.Utility == null || GlobalVariables.UseWeapon.Utility.ID != "Shoes")
                if(vel != Real3(0))
                    Utilities.SendSound(this.SprintSoundDist);
        }
        
        this.Owner.RigidBody.Velocity = vel;
        
    }
    
    function Freeze (time : Real) {
        this.FreezeTimer = time;
    }
    
}
