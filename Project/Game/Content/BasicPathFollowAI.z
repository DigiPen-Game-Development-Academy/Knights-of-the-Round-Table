//Author Brandon Wolenetz

class BasicPathFollowAI : ZilchComponent {
    
    var Timer : Real = 1;
    
    var Target : Real3;
    
    var TurnSpeed : Real = 360;
    
    function Initialize(init : CogInitializer) {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }

    function OnLogicUpdate(event : UpdateEvent) {
        
        this.Timer -= event.Dt;
        
        var tSpeed = Math.ToRadians(this.TurnSpeed);
        var angle = Math.ATan2(this.Owner.EnemyAI.ViewDirection.Y, this.Owner.EnemyAI.ViewDirection.X);
        
        this.Owner.RigidBody.Velocity = Math.Normalize(this.Owner.EnemyAI.ViewDirection);
        
        if(this.Owner.EnemyPathFind.Path != null && (this.Owner.EnemyPathFind.Path.Count <= 0 || this.Timer <= 0)){
            this.Timer = 1;
            this.Owner.EnemyPathFind.Recalculate(this.Target);
            if(this.Owner.EnemyPathFind.Path != null && this.Owner.EnemyPathFind.Path.Count <= 0){
                this.Owner.EnemyPathFind.Path = null;
            }
        }
        
        if(this.Owner.EnemyPathFind.Path == null){
            this.Owner.RigidBody.Velocity = Real3(0);
        } else {
            var tDir = this.Owner.EnemyPathFind.Path[0].WorldPos - this.Owner.Transform.WorldTranslation;
            var tAngle = Math.ATan2(tDir.Y, tDir.X);
            var a = Math.ATan2(Math.Sin(tAngle-angle), Math.Cos(tAngle-angle));
            if(Math.Abs(a - angle) < tSpeed*event.Dt)
                angle = tAngle;
            else if(a > 0) {
                angle += tSpeed*event.Dt;
            } else {
                angle += -tSpeed*event.Dt;
            }
            
            if(Math.Distance(this.Owner.EnemyPathFind.Path[0].WorldPos, this.Owner.Transform.WorldTranslation) < 0.1)
                this.Owner.EnemyPathFind.Recalculate(this.Target);
        }
        
        this.Owner.EnemyAI.ViewDirection = Real3(Math.Cos(angle), Math.Sin(angle), 0);
        
    }
    
    function SetTarget (target : Real3) {
        this.Target = target;
        if(this.Owner.EnemyPathFind.Path == null) {
            this.Timer = 1;
            this.Owner.EnemyPathFind.Recalculate(this.Target);
        }
    }
    
}
