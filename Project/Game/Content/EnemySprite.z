class EnemySprite : ZilchComponent {
    
    var LDir : Real3 = Real3(0);
    
    [Dependency]
    var AI : EnemyAI;
    
    [Property] var NorthSprite : SpriteSource;
    [Property] var SouthSprite : SpriteSource;
    [Property] var EastSprite  : SpriteSource;
    [Property] var WestSprite  : SpriteSource;
    
    [Property]
    var Cooldown : Real = 1;
    
    var Timer : Real = 1;
    
    function Initialize(init : CogInitializer) {
        this.Timer = this.Cooldown;
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }

    function OnLogicUpdate(event : UpdateEvent) {
        
        this.Timer -= event.Dt;
        
        if(this.Timer > 0) {
            this.AI.ViewDirection = this.LDir;
            return;
        }
        
        this.Timer = this.Cooldown;
        
        if(this.AI.ViewDirection != this.LDir && Math.Distance(this.AI.ViewDirection, Real3(0)) > 0.1) {
            
            var angle = Math.ToDegrees(Math.ATan2(this.AI.ViewDirection.Y, this.AI.ViewDirection.X));
            if(angle < 45 && angle > -45) {
                this.Owner.Sprite.SpriteSource = this.EastSprite;
            } else if(angle < -45 && angle > -135) {
                this.Owner.Sprite.SpriteSource = this.SouthSprite;
            } else if((angle < 225 && angle > 135) || (angle < -135 && angle > -225)) {
                this.Owner.Sprite.SpriteSource = this.WestSprite;
            } else if(angle < 135 && angle > 45) {
                this.Owner.Sprite.SpriteSource = this.NorthSprite;
            }
            
        }
        this.LDir = this.AI.ViewDirection;
    }
}
